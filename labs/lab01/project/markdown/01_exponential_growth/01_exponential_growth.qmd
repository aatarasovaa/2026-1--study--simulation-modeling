# Модель экспоненциального роста

**Цель:** Исследовать решение уравнения $\frac{du}{dt} = \alpha u$.

Экспоненциальный рост — это процесс увеличения величины, при котором
скорость роста пропорциональна текущему значению [стр. 69].

## Инициализация проекта и загрузка пакетов

```{julia}
using DrWatson
@quickactivate "project"

using DifferentialEquations
using Plots
using DataFrames
using JLD2
```

## Определение модели

Уравнение экспоненциального роста:
```math
\frac{du}{dt} = \alpha u, \quad u(0) = u_0
```

```{julia}
function exponential_growth!(du, u, p, t)
    α = p
    du[1] = α * u[1]
end
```

## Первый запуск с параметрами по умолчанию

Зададим начальные параметры:
- $u_0 = [1.0]$ — начальная популяция
- $\alpha = 0.3$ — скорость роста
- $t \in [0, 10]$ — временной интервал

```{julia}
u0 = [1.0]
α = 0.3
tspan = (0.0, 10.0)
```

Создадим и решим задачу:

```{julia}
prob = ODEProblem(exponential_growth!, u0, tspan, α)
sol = solve(prob, Tsit5(), saveat=0.1)
```

## Визуализация результатов

Построим график решения:

```{julia}
plot(sol,
     label="u(t)",
     xlabel="Время t",
     ylabel="Популяция u",
     title="Экспоненциальный рост (α = $α)",
     lw=2,
     legend=:topleft)
```

Сохраним график в папку plots:

```{julia}
savefig(plotsdir("exponential_growth_α=$α.png"))
```

## Анализ результатов

Создадим таблицу с данными:

```{julia}
df = DataFrame(t=sol.t, u=first.(sol.u))
println("Первые 5 строк результатов:")
println(first(df, 5))
```

Вычислим время удвоения популяции:
Время удвоения вычисляется по формуле $T_2 = \ln(2)/\alpha$ [стр. 70].

```{julia}
u_final = last(sol.u)[1]
doubling_time = log(2) / α
println("\nАналитическое время удвоения: ", round(doubling_time; digits=2))
```

## Сохранение результатов

Сохраним все данные для последующего анализа:

```{julia}
@save datadir("exponential_growth_results.jld2") df
```

